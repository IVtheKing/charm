/********************************************************************************
	
						Copyright 2012-2013 xxxxxxx, xxxxxxx
	File:	memmap.ld
	Author:	Bala B. (bhat.balasubramanya@gmail.com)
	Description: Linker script for the OS image
	
********************************************************************************/

OUTPUT_ARCH(arm)
ENTRY(_start)

MEMORY 
{
/*	SRAM		: ORIGIN = 0x40000000,  LENGTH = 0x1000	
	DRAM		: ORIGIN = 0x30000000,  LENGTH = 0x4000000	
*/

	OS_MEM		: ORIGIN = 0x30000000,  LENGTH = 0x100000	
	APP_MEM		: ORIGIN = 0x30100000,  LENGTH = 0x3F00000	
}

PHDRS
{
   code_seg		PT_LOAD;
   rodata_seg	PT_LOAD;
   data_seg		PT_LOAD;
}

SECTIONS
{
	.text :
	{
		*(.text.vector)
		*(.text.startup)
		*(.text)
		*(.text.*)	
		
	} > OS_MEM : code_seg
	
	. = ALIGN(4096);
	.rodata :
	{
		*(.rodata)
		*(.rodata.*)
			
	} > OS_MEM : rodata_seg
	
	. = ALIGN(4096);
	.data :
	{
		*(.data)
		
	} > OS_MEM : data_seg
	
	.bss :
	{
		. = ALIGN(4);
		__bss_start__ = .;
		*(.bss)
		*(COMMON)
		. = ALIGN(4);
		__bss_end__ = .;
		
	} > OS_MEM : data_seg
	
	.stack :
	{
		*(.stack)
						
	} > OS_MEM : data_seg
}
